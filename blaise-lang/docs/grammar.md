# BLAISE Grammar

```
ENTRY_POINT := PROGRAM | MODULE

PROGRAM := 'program' IDENTIFIER ';' SUBROUTINE* VARIABLES_CLAUSES? '.' COMPOUND_STATEMENT
MODULE := # WORK IN PROGRESS

IDENTIFIER := /[a-zA-z][a-zA-z0-9_]*/
NUMBER := /[0-9]+(\\.[0-9]+(f|d)?)?/ | 0b[0-1]+ | 0o[0-7]+ | 0x[0-9A-Fa-f]+
STRING_LITERAL := /"([^"]|\\.|\\u[0-9a-fA-F]{4})*"/
CHAR_LITERAL := /'([^']|\\.|\\u[0-9a-fA-F]{4})'/
BOOLEAN_LITERAL := 'true' | 'false'

VARIABLES_CLAUSES := 'var' VARIABLE_CLAUSE*
VARIABLE_CLAUSE :=  VARIABLE_NAME_LIST ':' VARIABLE_TYPE ';'
VARIABLE_NAME_LIST := IDENTIFIER (',' IDENTIFIER)*
VARIABLE_TYPE := ('unsigned'? 'byte')|('unsigned'? 'integer')|('unsigned'? 'long')|
                 'char'|'string'|
                 'float'|'double'|
                 'boolean'

COMPOUND_STATEMENT := 'begin' STATEMENT* 'end'
STATEMENT => (COMPOUND_STATEMENT | FUNCTION_CALL | ASSIGNMENT) ';' # WORK IN PROGRESS

FUNCTION_CALL := IDENTIFIER '(' FUNCTION_CALL_PARAMETERS? ')'
FUNCTION_CALL_PARAMETERS := EXPRESSION (',' EXPRESSION)*

ASSIGNMENT = IDENTIFIER ':=' EXPRESSION

# Expression idea from https://en.wikipedia.org/wiki/Operator-precedence_parser
# but I added a few more levels and operator
EXPRESSION := OR_EXPRESSION
OR_EXPRESSION := AND_EXPRESSION ( 'eager'? 'or' AND_EXPRESSION)*
AND_EXPRESSION := EQUALITY_EXPRESSION ( 'eager'? 'and' EQUALITY_EXPRESSION)*
EQUALITY_EXPRESSION := INEQUALITY_EXPRESSION (('==' | '<>' ) INEQUALITY_EXPRESSION)*
INEQUALITY_EXPRESSION := ADDITIVE_EXPRESSION (('<' | '<=' | '>=' | '>') ADDITIVE_EXPRESSION )*
ADDITIVE_EXPRESSION := MULTIPLICATIVE_EXPRESSION (('+' | '-' ) MULTIPLICATIVE_EXPRESSION )*
MULTIPLICATIVE_EXPRESSION := EXPRESSION_TERM (('*' | '/' | '%' ) EXPRESSION_TERM)*
EXPRESSION_TERM := '(' EXPRESSION ')' | NUMBER | IDENTIFIER | FUNCTION_CALL | '-' EXPRESSION | 'not' EXPRESSION | STRING_LITERAL | CHAR_LITERAL | BOOLEAN_LITERAL

# Sub-routines
SUBROUTINE := (FUNCTION_SUBROUTINE_SIGNATURE | PROCEDURE_SUBROUTINE_SIGNATURE) COMPOUND_STATEMENT? ';'
FUNCTION_SUBROUTINE_SIGNATURE := 'function' ('native')? IDENTIFIER '(' SUBROUTINE_PARAMETERS? ')' ':' VARIABLE_TYPE
PROCEDURE_SUBROUTINE_SIGNATURE := 'procedure' ('native')? IDENTIFIER '(' SUBROUTINE_PARAMETERS? ')'
SUBROUTINE_PARAMETERS := SUBROUTINE_PARAMETER (',' SUBROUTINE_PARAMETER)*
SUBROUTINE_PARAMETER := VARIABLE_NAME_LIST ':' VARIABLE_TYPE
```
